buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'http://maven.ferenyr.info/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id "java-gradle-plugin"
}

group = 'net.opmcorp'
version = '0.1.0'

apply plugin: 'groovy'
apply plugin: 'java'

//IDE
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'commons-io:commons-io:2.5'
    compile 'com.google.guava:guava:22.0'
    compile 'com.google.code.gson:gson:2.8.1'

    testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
    mavenLocal()
}

// Publications

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = 'opmcorp'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact (sourcesJar) {
                classifier = 'sources'
            }
            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
        }
    }
}